<!DOCTYPE html>
<html lang="en">
<head>
  <script defer type="x-shader/x-fragment" id="feedback-shader">
    #ifdef GL_ES
    precision mediump float;
    #endif

    const float SHAPE_FACTOR = .01;
    const float SHAPE_SIZE = 1.9;
    const float SHAPE_ANIMATION_SPEED = 2.0;
    const float SHAPE_PERIOD_FACTOR = 40.0;
    const float FADE_FACTOR = .995;
    const float EXPANSION_FACTOR = 1.1;
    const float SHAPE_HORIZONTAL_OSCILLATION_SPEED = .3;
    const float SHAPE_VERTICAL_OSCILLATION_SPEED = .2;
    const float SPECTRUM_SHIFT_SPEED = .2;
    const float ANGLE_ROTATION_SPEED = .1;
    const float FOLDING1_COUNT = 3.;
    const float FOLDING1_BASE = 1.;
    const float FOLDING1_FACTOR = .1;
    const float FOLDING1_ROTATION_SPEED = 1.2;
    const float FOLDING2_COUNT = 7.;
    const float FOLDING2_BASE = 1.;
    const float FOLDING2_FACTOR = .15;
    const float FOLDING2_ROTATION_SPEED = -.9;

    #define iResolution R
    #define iMinDimension D
    #define iTime T
    #define iFrame F
    #define iChannel0 T0

    uniform float T;
    uniform vec2 R;
    uniform float D;
    uniform int F;
    uniform sampler2D T0;

    vec3 spectrum(float x) {
      vec3 c = vec3(x - .75, x - .5,	x - .25) * 4.;
      return max(vec3(1) - c * c, vec3(0));
    }

    float wave(float x) {
      return (cos(x) + 1.) * .5;
    }

    void main() {
      vec2 uv = gl_FragCoord.xy / iResolution;
      vec2 st = (gl_FragCoord.xy * 2. - iResolution) / iMinDimension;
      vec2 expansion = st / iResolution * EXPANSION_FACTOR;
      vec2 shift = vec2(
        sin(iTime * SHAPE_HORIZONTAL_OSCILLATION_SPEED),
        cos(iTime * SHAPE_VERTICAL_OSCILLATION_SPEED)
      );
      float angle = atan(st.x, st.y) - iTime * ANGLE_ROTATION_SPEED;
      float dist = length(st - shift);
      float level = dist
        + ((cos(angle * FOLDING1_COUNT + iTime * FOLDING1_ROTATION_SPEED) + FOLDING1_BASE) * FOLDING1_FACTOR)
        + ((cos(angle * FOLDING2_COUNT + iTime * FOLDING2_ROTATION_SPEED) + FOLDING2_BASE) * FOLDING2_FACTOR);
      vec3 prevColor = texture2D(iChannel0, uv - expansion).rgb;
      float luma = wave((level * SHAPE_PERIOD_FACTOR) - (iTime * SHAPE_ANIMATION_SPEED));
      vec3 color = vec3(luma)
        * SHAPE_FACTOR
        * spectrum(mod(dist - iTime * SPECTRUM_SHIFT_SPEED, 1.))
        * step(level, SHAPE_SIZE);
      gl_FragColor = vec4(prevColor * FADE_FACTOR + color, 1.);
    }
  </script>
  <script defer type="x-shader/x-fragment" id="screen-shader">
    #ifdef GL_ES
    precision mediump float;
    #endif

    uniform vec2 R;
    uniform sampler2D T;
    varying vec2 vUV;

    void main() {
      gl_FragColor = texture2D(T, gl_FragCoord.xy / R);
    }
  </script>
  <script src="../shader-web-background.min.js"></script>
  <script>
    window.addEventListener("load", e => {
      shaderCanvas("background", "feedback-shader", "screen-shader");
    });
  </script>
  <link rel="stylesheet" href="test.css" />
</head>
<body>
<canvas id="background"></canvas>
</body>
</html>
